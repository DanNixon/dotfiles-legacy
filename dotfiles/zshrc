# The following lines were added by compinstall

zstyle ':completion:*' completer _expand _complete _ignored _approximate
zstyle ':completion:*' completions 1
zstyle ':completion:*' format '%d'
zstyle ':completion:*' group-name ''
zstyle ':completion:*' list-colors ${(s.:.)LS_COLORS}
zstyle ':completion:*' matcher-list 'm:{[:lower:]}={[:upper:]}'
zstyle ':completion:*' menu select=1
zstyle ':completion:*' select-prompt %SScrolling active: current selection at %p%s
zstyle ':completion:*' verbose true
zstyle :compinstall filename '/home/dan/.zshrc'

autoload -Uz compinit
compinit
# End of lines added by compinstall

# History
HISTFILE=~/.zsh_history
HISTSIZE=10000
SAVEHIST=10000

# Vi style key bindings
bindkey -v

function zle-line-init zle-keymap-select {
  PROMPT_VI_MODE="${${KEYMAP/vicmd/N}/(main|viins)/I}"
  PROMPT_VI_COLOR="${${KEYMAP/vicmd/blue}/(main|viins)/green}"
  zle reset-prompt
}

zle -N zle-line-init
zle -N zle-keymap-select

# History search
autoload -U history-search-end
zle -N history-beginning-search-backward-end history-search-end
zle -N history-beginning-search-forward-end history-search-end
bindkey "^[[A" history-beginning-search-backward-end
bindkey "^[[B" history-beginning-search-forward-end

# Prompts
if (( ${+SSH_CONNECTION} )); then
  PROMPT_HOSTNAME_COLOR='magenta'
else
  PROMPT_HOSTNAME_COLOR='yellow'
fi

setopt PROMPT_SUBST
PROMPT_GREY='248'
PROMPT='%B%F{cyan}%n%f%b %F{$PROMPT_GREY}at%f %B%F{$PROMPT_HOSTNAME_COLOR}%m%f%b %F{$PROMPT_GREY}in%f %B%F{blue}%~%f%b %F{$PROMPT_GREY}[%B%F{$PROMPT_VI_COLOR}$PROMPT_VI_MODE%F{$PROMPT_GREY}%b]%B>%f%b '
RPROMPT='%(?..%F{red}%?  )%F{240}%D{%Y-%m-%d %H:%M:%S}%f'

# Terminal title
case "$TERM" in
  *xterm*)
    precmd () { print -Pn "\e]2;$(pwd)\a" }
    ;;
esac

# Shared config
source "$HOME/dotfiles/dotfiles/shell/base16.sh"
source "$HOME/dotfiles/dotfiles/shell/aliases.sh"
source "$HOME/dotfiles/dotfiles/shell/environment.sh"
source "$HOME/dotfiles/dotfiles/shell/local_customizations.sh"

# Fzf
[ -f $HOME/.fzf.zsh ] && source $HOME/.fzf.zsh
